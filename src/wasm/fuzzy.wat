(module
  (memory (export "memory") 4)
  (global $heap (mut i32) (i32.const 0))
  (func (export "resetHeap")
    (global.set $heap (i32.const 0)))
  (func (export "alloc") (param $len i32) (result i32)
    (local $ptr i32)
    (local.set $ptr (global.get $heap))
    (global.set $heap (i32.add (global.get $heap) (local.get $len)))
    (return (local.get $ptr)))
  (func (export "score") (param $qPtr i32) (param $qLen i32) (param $cPtr i32) (param $cLen i32) (result f32)
    (local $i i32)
    (local $qIndex i32)
    (local $score i32)
    (local $consec i32)
    (local $qByte i32)
    (local $cByte i32)
    (if (result f32)
      (i32.eqz (local.get $qLen))
      (then (f32.const 0))
      (else
        (block $exit
          (loop $loop
            (br_if $exit (i32.ge_u (local.get $i) (local.get $cLen)))
            (br_if $exit (i32.ge_u (local.get $qIndex) (local.get $qLen)))
            (local.set $cByte (i32.load8_u (i32.add (local.get $cPtr) (local.get $i))))
            (local.set $qByte (i32.load8_u (i32.add (local.get $qPtr) (local.get $qIndex))))
            (if (i32.eq (local.get $cByte) (local.get $qByte))
              (then
                (if (local.get $consec)
                  (then (local.set $score (i32.add (local.get $score) (i32.const 2))))
                  (else (local.set $score (i32.add (local.get $score) (i32.const 1)))))
                (local.set $qIndex (i32.add (local.get $qIndex) (i32.const 1)))
                (local.set $consec (i32.const 1))
              )
              (else (local.set $consec (i32.const 0))))
            (local.set $i (i32.add (local.get $i) (i32.const 1)))
            (br $loop)
          )
        )
        (return
          (f32.div
            (f32.convert_i32_u (local.get $score))
            (f32.convert_i32_u (local.get $qLen))
          )
        )
      )
    )
  )
)
